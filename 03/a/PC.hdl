// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // If reset, inc or load is active, value of register changes
    Or(a=reset, b=inc, out=resetOrInc);
    Or(a=resetOrInc, b=load, out=resetOrIncOrLoad);
   
    // Inc Gate
    Inc16(in=feedback, out=feedbackInc);
    Mux16(a=feedback, b=feedbackInc, sel=inc, out=t1);

    // Load Gate
    Mux16(a=t1, b=in, sel=load, out=t2);

    // Reset Gate
    Mux16(a=t2, b=false, sel=reset, out=t3);
    
    Register(in=t3, load=resetOrIncOrLoad, out=feedback, out=out);
}
